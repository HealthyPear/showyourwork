import snakemake
from pathlib import Path
import sys


# Require Snakemake >= this version
snakemake.utils.min_version("6.7.0")


# Add our utils module to the path
HERE = Path(snakemake.workflow.workflow.current_basedir).absolute()
sys.path.insert(1, str(HERE.parents[0]))
from utils import paths


# Working directory is the top level of the user repo
workdir: paths.user.as_posix()


# This is the config file generated by the `preprocess` workflow
configfile: (paths.temp / "config.json").as_posix()


# Workflow report template
report: "report/workflow.rst"


# The main rule for this workflow
rule pdf:
    """
    Compile the manuscript into the article PDF.

    """
    input:
        config["ms_tex"],
        config["pdf_dependencies"],
        "showyourwork.yml"
    output:
        config["ms_pdf"],
        temp(config["tex_files_out"]),
        temp(config["stylesheet"]),
        temp(config["stylesheet_meta_file"]),
        temp(directory(paths.compile.as_posix()))
    conda:
        "envs/main.yml"
    script:
        "scripts/pdf.py"


# Include all other rules
rule_files = [f"rules/{file.name}" for file in paths.rules.glob("*.smk")]
for rule_file in rule_files:
    include: rule_file


# Include all checkpoints
checkpoint_files = [f"checkpoints/{file.name}" for file in paths.checkpoints.glob("*.smk")]
for checkpoint_file in checkpoint_files:
    include: checkpoint_file