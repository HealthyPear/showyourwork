
include: "helpers.smk"


rule pdf:
    input:
        "tex/ms.tex",
        glob("tex/*.bib"),
        TEMP / PROJECT / "meta.json",
        figures
    output:
        "ms.pdf"
    run:
        run_pdf()


rule figure:
    input:
        figure_script,
        "environment.yml"
    output:
        "{figure}"
    wildcard_constraints:
        figure="figures/(.*?)\.{}".format("|".join(FIGURE_EXTENSIONS))
    params:
        script_name=script_name,
        cached_figure=cached_figure,
        cache_cmd=cache_cmd
    conda:
        "environment.yml"
    shell:
        ("cd figures && "
        "[ -f {params.cached_figure} ] && "
        "{{ mv {params.cached_figure} . ; }} || "
        "{{ python {params.script_name} && {params.cache_cmd} ; }}")


rule user_info:
    output:
        TEMP / "user" / "user.json"
    priority: 99
    run:
        run_user_info()


rule repo_info:
    output:
        TEMP / PROJECT / "repo.json"
    priority: 98
    run:
        run_repo_info()


checkpoint script_info:
    input:
        "tex/ms.tex"
    output:
        TEMP / PROJECT / "scripts.json"
    run:
        run_script_info()


rule metadata:
    input:
        TEMP / "user" / "user.json",
        TEMP / PROJECT / "repo.json",
        TEMP / PROJECT / "scripts.json"
    output:
        TEMP / PROJECT / "meta.json"
    run:
        run_metadata()
